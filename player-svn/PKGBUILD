# Maintainer: 
pkgname=player-svn
pkgver=9127
pkgrel=1
pkgdesc="Cross-platform robot device interface & server"
arch=('i686')
url="http://playerstage.sourceforge.net/"
license=('GPL')
depends=('python2' 'libltdl' 'gsl' 'ruby' 'libusb-compat' 'boost-libs' \
    'opencv' 'libgnomecanvas')
makedepends=('subversion' 'cmake')
provides=('player')
conflicts=('player')
options=(debug !strip)
source=('player::svn://svn.code.sf.net/p/playerstage/svn/code/player/trunk')
md5sums=('SKIP')

pkgver() {
    cd "$srcdir/player"
    local ver="$(svnversion)"
    printf "%s" "${ver//[[:alpha:]]}"
}

build() {
    # https://bugs.archlinux.org/task/14023

    cd "$srcdir/player"
    cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_SKIP_RPATH=ON \
        -DPYTHON_EXECUTABLE=/usr/bin/python2 \
        -DPYTHON_INCLUDE_DIR=/usr/include/python2.7 \
        -DPYTHON_LIBRARY=/usr/lib/libpython2.7.so \
        -DPLAYERCC_EXTRA_LINK_LIBRARIES=boost_system \
        -DCMAKE_BUILD_TYPE=Debug
    make
}

package() {
    cd "$srcdir/player"
    make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
