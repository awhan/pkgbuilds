# Maintainer: awhan patnaik
pkgname=player-svn
pkgver=9136 
pkgrel=1
pkgdesc="Cross-platform robot device interface & server"
arch=('i686')
url="http://playerstage.sourceforge.net/"
license=('GPL')
depends=('python2')
makedepends=('subversion' 'cmake')
provides=('player')
conflicts=('player')
options=()
source=('player::svn://svn.code.sf.net/p/playerstage/svn/code/player/trunk')
md5sums=('SKIP')

pkgver() {
    cd "$srcdir/player"
    local ver="$(svnversion)"
    printf "%s" "${ver//[[:alpha:]]}"
}

build() {
    # https://bugs.archlinux.org/task/14023
    cd "$srcdir/player"
    cmake -DCMAKE_INSTALL_PREFIX=/usr \
    -DPYTHON_EXECUTABLE=/usr/bin/python2
    make 
}

package() {
    cd "$srcdir/player"
    make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:

