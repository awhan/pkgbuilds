# -*- mode:Shell-script -*-
# Maintainer: awhan patnaik
pkgname=player-svn-dbg
pkgver=9136
pkgrel=1
pkgdesc="Cross-platform robot device interface & server"
arch=('i686')
url="http://playerstage.sourceforge.net/"
license=('GPL')
depends=('freeglut' 'python2' 'libltdl' 'gsl' 'ruby' 'libusb-compat' 'boost-libs' \
    'opencv' 'libgnomecanvas')
makedepends=('subversion' 'cmake')
provides=('player')
conflicts=('player')
options=(debug !strip)
source=('repo::svn://svn.code.sf.net/p/playerstage/svn/code/player/trunk')
md5sums=('SKIP')

pkgver() {
    cd "$srcdir/repo"
    local ver="$(svnversion)"
    printf "%s" "${ver//[[:alpha:]]}"
}

prepare() {
    cd "$srcdir/repo"
    while IFS=: read -r file line ignored; do
        ed "$file" <<< "$line"$'s/python/python2\nw\nq'
    done < <(grep -rn --exclude-dir='.svn' '#!/usr/bin/env python')
}


build() {
    # insecure RPATH related https://bugs.archlinux.org/task/14023

    cd "$srcdir/repo"
    cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_SKIP_RPATH=ON \
        -DPYTHON_EXECUTABLE=/usr/bin/python2 \
        -DPYTHON_INCLUDE_DIR=/usr/include/python2.7 \
        -DPYTHON_LIBRARY=/usr/lib/libpython2.7.so \
        -DCMAKE_BUILD_TYPE=Debug
    make
}

package() {
    cd "$srcdir/repo"
    make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
