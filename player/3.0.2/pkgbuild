# Maintainer:
pkgname=player
pkgver=3.0.2
pkgrel=1
pkgdesc="Cross-platform robot device interface & server"
arch=('i686')
url="http://playerstage.sourceforge.net"
license=('GPL')
depends=('libgnomecanvas' 'libtool' 'libusb-compat' 'boost-libs' 'gsl' 'opencv' 'python2')
makedepends=('cmake')
provides=('player')
conflicts=('player')
source=("http://downloads.sourceforge.net/project/playerstage/Player/${pkgver}/${pkgname}-${pkgver}.tar.gz")
md5sums=('b92b5ea028e6bfc49351849f420167db')
options=('debug' '!strip')

prepare() {
    cd "$srcdir/$pkgname-$pkgver"
    ed -s client_libs/libplayerc++/playerclient.cc <<< $'171s/UTC/UTC_/\nw\nq'
    ed -s server/drivers/shell/readlog.cc <<< $'668s/file/gzfile/\nw\nq'
    ed -s server/drivers/shell/readlog.cc <<< $'714s/file/gzfile/\nw\nq'
}

build() {
    cd "$srcdir/$pkgname-$pkgver"
    cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_SKIP_BUILD_RPATH=TRUE \
        -DPLAYERCC_EXTRA_LINK_LIBRARIES=boost_system \
        -DPYTHON_EXECUTABLE=/usr/bin/python2 \
        -DCMAKE_BUILD_TYPE=Debug
    make
}

package() {
    cd "$srcdir/$pkgname-$pkgver"
    make DESTDIR="$pkgdir/" install
}
