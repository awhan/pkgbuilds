# -*- mode:Shell-script -*-
# Maintainer: awhan patnaik
pkgname=player
pkgver=2.1.2
pkgrel=1
pkgdesc="Cross-platform robot device interface & server"
arch=('i686')
url="http://playerstage.sourceforge.net"
license=('GPL')
#depends=('libgnomecanvas' 'libtool' 'libusb-compat' 'boost-libs' 'gsl' 'opencv' 'python2')
depends=('freeglut' 'libusb-compat' 'libgnomecanvas' 'libltdl' 'boost-libs' 'gsl' 'opencv' 'python2')
makedepends=('cmake')
provides=('player')
conflicts=('player')
source=("http://downloads.sourceforge.net/project/playerstage/Player/${pkgver}/${pkgname}-${pkgver}.tar.bz2"
    'fixit')
options=()

prepare() {
    cd "$srcdir/$pkgname-$pkgver"
    
    ../fixit .

    while IFS=: read -r file line _; do
        ed "$file" <<< "$line"$'s/python/python2\nw\nq'
        #echo "$line"
    done < <(grep -Ern --include=Makefile.am "	python ")

    while read -r file; do
        ed "$file" <<< $'1s/python/python2\nw\nq'
    done < <(grep -rl '#!/usr/bin/env python')
}

build() {
    cd "$srcdir/$pkgname-$pkgver"
    autoreconf -i
    ./configure --prefix=/usr --disable-rpath
    make
}

package() {
    cd "$srcdir/$pkgname-$pkgver"
    make DESTDIR="$pkgdir/" install
}


md5sums=('324cb05252c916d4ce6106d6c83b6131'
         '684fe8256c671cdc3dc2dfe82aaa2115')
